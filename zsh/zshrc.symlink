# shortcut to this dotfiles path is $ZSH
export ZSH=$HOME/.dotfiles

# your project folder that we can `c [tab]` to
export PROJECTS=~/Code

# Stash your environment variables in ~/.localrc. This means they'll stay out
# of your main dotfiles repository (which may be public, like this one), but
# you'll have access to them in your scripts.
if [[ -a ~/.localrc ]]
then
  source ~/.localrc
fi

# all of our zsh files
typeset -U config_files
config_files=($ZSH/**/*.zsh)

# load the path files
for file in ${(M)config_files:#*/path.zsh}
do
  source $file
done

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
  source $file
done

# initialize autocomplete here, otherwise functions won't be loaded
autoload -U compinit
compinit

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
  source $file
done

# Node, this prob shouldn't go here
nvm_init () {
  export NVM_DIR=~/.nvm
  export NVM_NODEJS_ORG_MIRROR=http://nodejs.org/dist
  source $(brew --prefix nvm)/nvm.sh
  nvm use default
}
#
# Start the nvm with this version, uncomment to use by default
#nvm_init

# ?? why..
export CXX=g++-4.2

# Lower <ESC> delay from .4 default to .1, might mess something else up
export KEYTIMEOUT=1

unset config_files
